plugins {
    alias libs.plugins.neogradle.userdev
    alias libs.plugins.neogradle.mixin
}

sourceSets {
    main.runs.modIdentifier = project.modid
    test.runs.modIdentifier = project.modid+'tests'
}

runs {
    configureEach {
        systemProperty 'forge.logging.console.level', 'debug'
        programArguments.addAll '--mixin.config', 'biomesquisher.test.mixins.json'
        modSources.add project.sourceSets.main
        modSources.add project.sourceSets.test
    }

    client {}

    server {
        programArgument '--nogui'
    }

    gameTestServer {
        programArgument '--nogui'
    }
}

mixin {
    configs.add 'biomesquisher.mixins.json'
}

jarJar.enable()

configurations {
    minecraftLibrary
    minecraftRuntime
    implementation.extendsFrom minecraftLibrary
    minecraftRuntime.extendsFrom minecraftLibrary
}

dependencies {
    implementation libs.neoforge

    compileOnly project(":common")
    testCompileOnly project(":common")
    testCompileOnly project(":common").sourceSets.test.output

    minecraftLibrary libs.pngj
    jarJar(libs.pngj.get()) {
        jarJar.ranged(it, "[${libs.versions.pngj.get()},)")
    }

    jarJar(libs.mixinextras.neoforge.get()) {
        jarJar.ranged(it, "[${libs.versions.mixinextras.get()},)")
    }
    implementation libs.mixinextras.neoforge
}

import net.neoforged.gradle.userdev.runtime.tasks.ClasspathSerializer
tasks.withType(ClasspathSerializer).configureEach {
    it.inputFiles.from(configurations.minecraftRuntime)
}

processResources {
    from project(":common").sourceSets.main.resources
}

tasks.named('compileJava', JavaCompile).configure {
    source(project(":common").sourceSets.main.allSource)
}

import io.github.groovymc.modsdotgroovy.ConvertToTomlTask;
var testMdg = tasks.register('testModsDotGroovyToToml', ConvertToTomlTask) {
    input.set new File(project(':common').sourceSets.test.resources.srcDirs.find(), "mods.groovy")
    dependsOn(project(':common').configurations.modsDotGroovy)
    dslClasspath.from(project(':common').configurations.modsDotGroovy)
}

processTestResources {
    dependsOn testMdg
    exclude 'mods.groovy'
    from(testMdg.get().output.get().asFile) {
        into 'META-INF'
    }
    from project(":common").sourceSets.test.resources
}

tasks.named('compileTestJava', JavaCompile).configure {
    source(project(":common").sourceSets.test.allSource)
}

sourcesJar {
    from project(":common").sourceSets.main.allSource
}

javadocJar {
    from project(":common").sourceSets.main.allJava
}

tasks.named('build').configure {
    dependsOn 'jarJar'
}
